/*
 * Adria Standard Library
 *
 * Copyright (C) 2014 Dennis MÃ¶hlmann <mail@dennismoehlmann.de>
 * Licensed under the MIT license.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

/**
 * set type
 */
module Set = proto {

    constructor: func(value) {

        this.data = Object.create(null);

        if (value !== undefined) {
            this.add(value);
        }
    },

    /**
     * merge one or more sets with current and return result
     * does not modify this set
     *
     * @param Set, ... sets to merge
     * @return Set resulting Set
     */
    merge: func() {

        var args    = Array.prototype.slice.call(arguments, 0);
        var result  = new Set();

        for (var key in this.data) {
            result.data[key] = true;
        }

        for (var id in args) {
            for (var key in args[id].data) {
                result.data[key] = true;
            }
        }

        return result;
    },

    /**
     * add element, array of elements or Set to this Set
     *
     * @param value
     */
    add: func(value) {

        var data = this.data;

        if (value instanceof Array) {

            value.forEach(func(element) {
                data[element] = true;
            });

        } else if (value instanceof Set) {

            for (var element in value.data) {
                data[element] = true;
            }

        } else {

            data[value] = true;
        }

        return this;
    },

    /**
     * remove element, array of elements or Set from this Set
     *
     * @param value
     */
    remove: func(value) {

        var data = this.data;

        if (value instanceof Array) {

            value.forEach(func(element) {
                delete data[element];
            });

        } else if (value instanceof Set) {

            for (var element in value.data) {
                delete data[element];
            }

        } else {
            delete data[value];
        }

        return this;
    },

    /**
     * check if set has single value, all values of array or set
     *
     * @param value
     * @return boolean
     */
    has: func(value) {

        var data = this.data;

        if (value instanceof Array) {

            for (var id in value) {
                if (value[id] in data !== true) {
                    return false;
                }
            }

            return true;

        } else if (value instanceof Set) {

            var other = value.data;

            for (var key in other) {
                if (key in data !== true) {
                    return false;
                }
            }

            return true;

        } else {

            return (value in data);
        }
    },

    /**
     * check if set lacks single value, all values of array or set
     *
     * @param value
     * @return boolean
     */
    lacks: func(value) {
        return this.has(value) === false;
    },

    /**
     * returns key missing in this set to equal another set or array
     *
     * @param value
     * @return boolean
     */
    missing: func(value) {

        var result = new Set();
        var data = this.data;

        if (value instanceof Array) {

            for (var id in value) {
                if (data[value[id]] !== true) {
                    result.add(value[id]);
                }
            }

        } else if (value instanceof Set) {

            var other = value.data;

            for (var key in other) {
                if (data[key] !== true) {
                    result.add(key);
                }
            }

        } else {

            throw new Error('invalid argument');
        }

        return result;
    },

    /**
     * return set values as array
     *
     * @return Array
     */
    toArray: func() {
        return Object.keys(this.data);
    },

    /**
     * check if the set is empty
     *
     * @return boolean
     */
    empty: prop {
        get : func() {
            for (var dummy in this.data) {
                return false;
            }
            return true;
        },
    },

    /**
     * number of elements in set
     *
     * @return integer
     */
    length: prop {
        get : func() {
            var len = 0;
            for (var dummy in this.data) {
                len++;
            }
            return len;
        },
    },

};