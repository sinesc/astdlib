/*
 * Adria Standard Library
 *
 * Copyright (C) 2014 Dennis Möhlmann <mail@dennismoehlmann.de>
 * Licensed under the MIT license.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to
 * use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
 * the Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
 * FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
 * IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
 * CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

/**
 * map type
 */
module Map = proto {

    constructor: func(key, value) {

        this.data = Object.create(null);

        if (key !== undefined) {
            this.set(key, value);
        }
    },

    /**
     * merge one or more maps with current and return result
     * does not modify this map
     *
     * @param Map, ... maps to merge
     * @return Map resulting Map
     */
    merge: func() {

        var args    = Array::slice.call(arguments, 0);
        var result  = new Map();

        for (var key, value in this.data) {
            result.data[key] = value;
        }

        for (var id in args) {
            for (var key, value in args[id].data) {
                result.data[key] = value;
            }
        }

        return result;
    },

    /**
     * set element or add Map to this Map
     *
     * @param key string key or Map
     * @param value omit when key is a Map
     */
    set: func(key, value) {

        var data = this.data;

        if (key instanceof Map) {

            assert(value === undefined);

            for (var mapKey, mapValue in value.data) {
                data[mapKey] = mapValue;
            }

        } else {

            data[key] = value;
        }

        return this;
    },

    get: func(key) {

        return this.data[key];
    },

    /**
     * remove element, array of elements or Set from this Map
     *
     * @param key
     */
    unset: func(key) {

        var data = this.data;

        if (key instanceof Array) {

            for (var k, arrayValue in key) {
                delete data[arrayValue];
            }

        } /*else if (value instanceof Set) {

            for (var element in value.data) {
                delete data[element];
            }

        }*/ else {
            delete data[key];
        }

        return this;
    },

    /**
     * check if map has single key, all values of array or set
     *
     * @param key
     * @return boolean
     */
    has: func(key) {

        var data = this.data;

        if (key instanceof Array) {

            for (var id in key) {
                if (key[id] in data !== true) {
                    return false;
                }
            }

            return true;

        }/* else if (key instanceof Set) {

            var other = key.data;

            for (var otherKey in other) {
                if (otherKey in data !== true) {
                    return false;
                }
            }

            return true;

        }*/ else {

            return (key in data);
        }
    },

    /**
     * check if map lacks single key, all values of array or set
     *
     * @param key
     * @return boolean
     */
    lacks: func(key) {
        return this.has(key) === false;
    },

    /**
     * return map keys as array
     *
     * @return Array
     */
    keys: func() {
        return Object.keys(this.data);
    },

    /**
     * return map values as array
     *
     * @return Array
     */
    values: func() {

        var result = [];

        for (var key, value in this.data) {
            result.push(value);
        }

        return result;
    },

    /**
     * check if the map is empty
     *
     * @return boolean
     */
    empty: prop {
        get: func() {
            for (var dummy in this.data) {
                return false;
            }
            return true;
        },
    },

    /**
     * number of elements in map
     *
     * @return integer
     */
    length: prop {
        get : func() {
            var len = 0;
            for (var dummy in this.data) {
                len++;
            }
            return len;
        },
    },

};